options ps=60;


libname proj3 '/home/u62241961/BUMK742/Project 3';


/*********** Part I: Purchase Incidence Model ******************/
/*** Data: 'deterg.sas7bdat' ***/
data deterg;
set proj3.deterg;

/* Create category level variables for the purchase incidence model. */
/* These are the variables that may affect the attractiveness */
/* of the category. They are defined as follows.              */
/* avg_rp: average regular price of the category.             */ 
/* avg_pc: average price cut of the category.                 */ 
/* cat_disp: =1 if any brand in the category is on in-store   */
/*           display; 0 otherwise.                            */
/* cat_feat: =1 if any brand in the category is on feature    */
/*           advertising; 0 otherwise.                        */
avg_rp=mean(of regpr1-regpr4);
avg_pc=mean(of pcut1-pcut4);
cat_disp=0;
cat_feat=0;
if disp1=1 or disp2=1 or disp3=1 or disp4=1 then cat_disp=1;
if feat1=1 or feat2=1 or feat3=1 or feat4=1 then cat_feat=1;

/* Get descriptive statistics of key variables */
proc means;
var incid volume regpr1-regpr4 pcut1-pcut4 disp1-disp4 feat1-feat4 
    avg_rp avg_pc cat_disp cat_feat lbpromot avol;

proc freq;
table incid;     /* purchase incidence frequency */

proc freq;
table choice*incid;  /* brand shares, only need the tabulation when incid=1 */

proc means;
var volume;
class choice;    /* average purchase volume by brand */

/* Estimate a Binary logit model using PROC GENMOD */
proc genmod data=deterg descending;
     model incid = avg_rp avg_pc cat_disp cat_feat lbpromot / 
           dist=binomial link=logit; 
     title 'Binary Logit Model for Category Purchase Incidence = 1';
run;


/************** Part II: Brand Choice Model *******************/
/*** Data: 'choice_det.sas7bdat' ***/

data choice;
set proj3.choice_det;

intcpt1=0;
intcpt2=0;
intcpt3=0;
if brand=1 then intcpt1=1;
if brand=2 then intcpt2=1;
if brand=3 then intcpt3=1;

/* Get descriptive statistics of key variables */
proc means data=choice;
var regpr pcut disp feat;
class brand;
title;  /* suppress the previously defined title,otherwise stay in effect until redefined */

/* Estimate a multinomial logit (MNL) model for brand choice decisions. */
/* This model is called the "conditional logit model" in PROC MDC.*/
/* It is specified by defining TYPE=CLOGIT in the MODEL statement. */
proc mdc data=choice;
   model decision = intcpt1 intcpt2 intcpt3 regpr pcut disp feat /type=clogit
         choice=(brand 1 2 3 4);
   id caseid;
   title 'Multinomail Logit Model for Brand Choice';
run; 


/***************** Part III: Purchase Quantity Models ****************/
/* Estimating the quantity model for one brand at a time.            */
/* A semi-log model is estimated for each brand  */ 
/* A parsimonious model with only price cut promotion is estiamted for */
/* each brand here. You should test the effects of display and feature ad */
/* and modify your final model accordingly. */
/* Data: The observations in "deterg.sas7bdat" where brand k is chosen */

data temp1;
set deterg;
if choice=1;   /* keep only those observations when brand 1 was chosen */
logvol1=log(volume);
proc reg;
model logvol1 = avol regpr1 pcut1 lbpromot;
title 'Semi-log (conditional) purchase quantity model for brand 1';
run;

data temp2;
set deterg;
if choice=2;   /* keep only those observations when brand 2 was chosen */
logvol2=log(volume);
proc reg;
model logvol2=avol regpr2 pcut2 lbpromot;
title 'Semi-log (conditional) purchase quantity model for brand 2';
run;

data temp3;
set deterg;
if choice=3;   /* keep only those observations when brand 3 was chosen */
logvol3=log(volume);
proc reg;
model logvol3=avol regpr3 pcut3 lbpromot;
title 'Semi-log (conditional) purchase quantity model for brand 3';
run;

data temp4;
set deterg;
if choice=4;   /* keep only those observations when brand 4 was chosen */
logvol4=log(volume);
proc reg;
model logvol4=avol regpr4 pcut4 lbpromot;
title 'Semi-log (conditional) purchase quantity model for brand 4';
run;
